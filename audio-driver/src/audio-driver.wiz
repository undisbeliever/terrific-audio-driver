// Audio Driver
//
// SPDX-FileCopyrightText: © 2023 Marcus Rowe <undisbeliever@gmail.com>
// SPDX-License-Identifier: Zlib
//
// Copyright © 2023 Marcus Rowe <undisbeliever@gmail.com>
//
// This software is provided 'as-is', without any express or implied warranty.  In
// no event will the authors be held liable for any damages arising from the use of
// this software.
//
// Permission is granted to anyone to use this software for any purpose, including
// commercial applications, and to alter it and redistribute it freely, subject to
// the following restrictions:
//
//      1. The origin of this software must not be misrepresented; you must not
//         claim that you wrote the original software. If you use this software in
//         a product, an acknowledgment in the product documentation would be
//         appreciated but is not required.
//
//      2. Altered source versions must be plainly marked as such, and must not be
//         misrepresented as being the original software.
//
//      3. This notice may not be removed or altered from any source distribution.


import "registers";
import "data-formats";
import "io-commands";
import "common_memmap";

config {
    trim = true
}

bank code           @ CODE_ADDR             : [constdata; CODE_SIZE];
bank lastvarpage    @ CODE_ADDR + CODE_SIZE : [vardata; LAST_VAR_PAGE_SIZE];


extern const commonDataHeader @ COMMON_DATA_ADDR : CommonDataHeader;
let COMMON_DATA_HEADER_END_ADDR = COMMON_DATA_ADDR + sizeof(CommonDataHeader);


// Must wait a minimum of 240ms after setting `EDL` and `ESA` before reading/writing the echo buffer.
//
// Added a little extra to be safe.
let EDL_SLEEP_MS = 250;

// Instead of setting TIMER_0 register to 8 and counting `EDL_SLEEP_MS` clocks, we can set TIMER_0 to `EDL_SLEEP_MS` and wait 8 clocks.
let EDL_SLEEP_TIMER_0 = EDL_SLEEP_MS;
let EDL_SLEEP_COUNT   = smp.TIMER_0_HZ / 1000;

// Assert EDL_SLEEP_COUNT < 12



// Variables
// =========

let FIRST_MUSIC_CHANNEL = 0;
let LAST_MUSIC_CHANNEL = N_MUSIC_CHANNELS - 1;
let FIRST_SFX_CHANNEL = LAST_MUSIC_CHANNEL + 1;
let LAST_SFX_CHANNEL = N_CHANNELS - 1;


// Using separate values for slide and triangle to simplify the rust bytecode interpreter.
// See: `channelSoA.volEffect_direction`
let VOL_PAN_EFFECT_SLIDE_UP      = 0x80;
let VOL_PAN_EFFECT_SLIDE_DOWN    = 0x81;
let VOL_PAN_EFFECT_TRIANGLE_UP   = 0x40;
let VOL_PAN_EFFECT_TRIANGLE_DOWN = 0x41;


in zeropage {
    // Temporary variables
    var zpTmp : u8;
    var zpTmp2 : u8;


    // Common audio data pointers.
    // See `data-formats.wiz` for more details about these variables.
    var commonData : CommonDataPointers;


    // Increases once every song tick
    var songTickCounter : u16;


    // Enabled or disabled music channels.
    // A bitmask of the music channels that can send key-on events.
    //
    // This bitmask is set by the SET_MUSIC_CHANNELS IO comamnd, it is not `keyOnMask`.
    var io_musicChannelsMask : u8;


    // The previous IO port command
    var previousCommand : u8;


    // The table of subroutine offsets
    var subroutineTable_l : *const u8;
    var subroutineTable_h : *const u8;

    // Maximum value of the timer counter_0 or counter_1 register.
    var maxTimerCounter : u8;


    // bitmask - Set if S-DSP voice channel is music, clear if sfx.
    // Used to disable music channel S-DSP writes that are used by sound effects.
    // Bits 0-5 MUST be set.
    // MUST ONLY be modified by `_process_sfx_channels__inline()`.
    var musicSfxChannelMask : u8;

    // bitmask - Set if S-DSP voice channel is sfx, clear if music
    //           (opposite to `musicSfxChannelMask`).
    // Used to enabled/disable noise and echo when sound effects channels are active.
    // MUST ONLY be modified by `_process_sfx_channels__inline()`.
    var sfxMusicEchoNoiseMask : u8;

    // bitmask - Set if S-DSP voice channel is sfx.
    // Used to disable S-DSP voice register writes for unused sfx channels.
    // Used to determine the next available sound effect channel.
    var activeSoundEffects : u8;

    // The `activeSoundEffects` value in the previous sfx tick.
    var prevActiveSoundEffects : u8;

    // bitmask - Set if the sound effect is interruptible
    // (only uses bits 6 & 7)
    var sfx_interruptibleFlags : u8;


    // The remaining duration of the two sound effects in ticks
    var sfx_remainingTicks : [u16 ; N_SFX_CHANNELS];

    // Used to detect active one-channel sound effects.
    // This value MUST BE 0xff if the channel is disabled or the *one-channel* flag is clear.
    var sfx_oneChannelSfxId : [u8 ; N_SFX_CHANNELS];


    // S-DSP voice channels dirty flags.
    // Used to test if the virtualChannels should be written to the S-DSP voice registers.
    // (using a bitset so they can be masked when channel ducking)
    var voiceChannelsDirty_music : u8;
    var voiceChannelsDirty_sfx   : u8;


    // Shadow variables for KOFF DSP register
    var keyOffShadow_music : u8;
    var keyOffShadow_sfx : u8;

    // Shadow variables for KON DSP register
    var keyOnShadow_music : u8;
    var keyOnShadow_sfx : u8;

    // Shadow variable for the EON S-DSP register
    var eonShadow_music : u8;
    var eonShadow_sfx : u8;


    // `keyOnShadow` mask.
    //
    // Each bit determines the channel bytecode should emit a key-on event.
    //  * If set - the next play note instruction will set `keyOnShadow`.
    //  * If clear - the channel is playing something and the play note instruction will
    //               leave `keyOnShadow` unchanged (slurring the note).
    // (8x bitfield)
    var keyOnMask_music : u8;
    var keyOnMask_sfx : u8;


    // Only valid inside bytecode instructions
    var instructionPtr : *const u8;


    // Temporary variable for reading and writing `voiceChannelsDirty_*` bits.
    //
    // MUST ONLY be written to in `__process_channels()`.
    var voiceChannelsDirty_tmp  : u8;

    // The one-past-the-end index when looping through the channels.
    //
    // Used to deduplicate code when processing music and sound effects.
    //
    // MUST ONLY BE USED IN `__process_channels()`.
    var channelIndexEndLoop : u8;


    namespace channelSoA {
        // Counter (in ticks) until the event
        var countdownTimer : [u8 ; N_CHANNELS];

        // If non-zero, the next event is a key-off event
        // If zero, the next event will process bytecode
        var nextEventIsKeyOff : [u8 ; N_CHANNELS];


        // S-DSP voice register shadows (AKA Virtual Channels)
        namespace virtualChannels {
            var vol_l       : [u8 ; N_CHANNELS];
            var vol_r       : [u8 ; N_CHANNELS];
            var pitch_l     : [u8 ; N_CHANNELS];
            var pitch_h     : [u8 ; N_CHANNELS];
            var scrn        : [u8 ; N_CHANNELS];
            var adsr1       : [u8 ; N_CHANNELS];
            // If the MSB of adsr1 is set, this value is written to the ADSR2 register.
            // If the MSB of adsr1 is clear, this value is written to the GAIN register.
            var adsr2OrGain : [u8 ; N_CHANNELS];

            // Temporary GAIN
            //
            // If this variable is non-zero, it overrides `adsr1` and `adsr2OrGain`.
            // Reset to 0 on key-off.
            var tempGain : [u8 ; N_CHANNELS];
        }


        // The address of the next bytecode to execute.
        //
        // If the high-byte of the instructionPtr is 0, no bytecode will be executed.
        //
        // (The low-byte of instructionPtr is in firstpage)
        var instructionPtr_h : [u8 ; N_CHANNELS];


        // Stack pointer.
        // The stack grows downwards,
        // from highest address (BcStackIndexesStart[x]) to lowest address (BcStackIndexesEnd[x])
        //
        // CAUTION: Is out of bounds when the stack is empty.
        // CAUTION: `bcStack` label is offset by BC_CHANNEL_STACK_OFFSET.
        //
        // MUST always be >= `BcStackIndexesEnd[x]` and <= `(BcStackIndexesStart[x]`
        //
        // (index into `bcStack`)
        var stackPointer : [u8 ; N_CHANNELS];


        // The stack pointer to use in the `skip_last_loop` and `end_loop` instructions.
        //
        // Used to skip bounds checking when reading/modifying counter.
        //
        // CAUTION: Not equal to `stackPointer` if there is less than 2 or fewer bytes on the stack.
        //
        // MUST always be >= `BcStackIndexesEnd[x]` and <= `(BcStackIndexesStart[x] - 3)`
        //
        // (index into `bcStack`)
        var loopStackPointer : [u8 ; N_CHANNELS];


        // Channel volume (0-255)
        var volume : [u8 ; N_CHANNELS];


        // Portamento direction
        //  * zero - portamento disabled
        //  *  > 0 - increase pitch
        //  *  < 0 - decrease pitch
        var portamento_direction : [i8 ; N_CHANNELS];



        // Vibrato PITCH offset per tick.
        //
        // If this value is 0, vibrato is disabled.
        //
        // (pitch change/tick)
        var vibrato_pitchOffsetPerTick : [u8 ; N_CHANNELS];

        // Vibrato tick counter
        //  * Starts at `vibrato_tickCounterStart` to offset the vibrato pitch-shift
        //  * Counts down from `vibrato_waveLengthInTicks` to 0.
        //  * If 0, there are 256 ticks remaining
        var vibrato_tickCounter : [u8 ; N_CHANNELS];
    }


    // Used to determine if vol_l and vol_r need recalculating.
    // In the `__process_channels()` loop, bit 7 is left-shifted out and the volume is updated if set.
    // (bitfield)
    var volShadowDirty_tmp : u8;

    // The sound effects with invalid vol_l and vol_r shadow variables.
    // Set in the PLAY_SOUND_EFFECT command.
    // CAUTION: bits 6 & 7 are swapped from other the other *_sfx bitsets
    // (bitfield)
    var volShadowDirty_sfx : u8;


    // NON DSP register shadow
    var nonShadow_music : u8;
    var nonShadow_sfx : u8;

    // noise frequency register shadow variables
    // (Value is only written to the flag register if MSB clear)
    var noiseFreq_music : u8;
    var noiseFreq_sfx : u8;
}


in firstpage {
    // Channel variables that do not fit in zeropage
    namespace channelSoA {
        // Portamento PITCH speed
        // (pitch change/tick)
        var portamento_speed : [u8 ; N_CHANNELS];

        // Portamento target pitch
        var portamento_target_l : [u8 ; N_CHANNELS];
        var portamento_target_h : [u8 ; N_CHANNELS];


        // Used to determine the vibrato pitch shift direction for the current vibrato tick counter.
        //
        // This variable is equal to `vibrato_waveLengthInTicks / 2`
        var vibrato_directionComparator : [u8 ; N_CHANNELS];

        // The length of the vibrato wavelength in ticks.
        //
        // If this value is 0. The wavelength is 256 ticks.
        //
        // This variable is equal to `4 * vibrato_tickCounterStart`
        var vibrato_waveLengthInTicks : [u8 ; N_CHANNELS];

        // The value to reset the tick counter to when playing a note or starting a vibrato.
        //
        // This variable is equal to the `quarterWavelengthInTicks` parameter of the `set_vibrato` instruction
        // This variable is equal to `vibrato_waveLengthInTicks / 4`
        //
        // Offsetting the start of the vibrato allows me to save a comparison in `_process_vibrato__inline()`.
        var vibrato_tickCounterStart : [u8 ; N_CHANNELS];


        // The address of the next bytecode to execute
        // (The high-byte of instructionPtr is in firstpage)
        var instructionPtr_l : [u8 ; N_CHANNELS];


        // Fractional volume (used for volume effects)
        var subVolume : [u8 ; N_CHANNELS];

        // Volume effect direction.
        //  * zero - volume effect disabled
        //  * bit 0 clear - increment volume
        //  * bit 0 set - decrement volume
        var volEffect_direction : [u8 ; N_CHANNELS];

        // Countdown ticks for volume effects
        // (ticks)
        var volEffect_counter : [u8 ; N_CHANNELS];

        // Volume offset per tick
        // (8.8 unsigned fixed point)
        var volEffect_offset_l : [u8 ; N_CHANNELS];
        var volEffect_offset_h : [u8 ; N_CHANNELS];

        // Tremolo half-wavelength in ticks.
        // If 0, the volume effect is a volume slide.
        var volEffect_halfWavelength : [u8 ; N_CHANNELS];


        // Channel pan (0-MAX_PAN)
        var pan : [u8 ; N_CHANNELS];

        // See volEffect variables
        var subPan : [u8 ; N_CHANNELS];
        var panEffect_direction : [u8 ; N_CHANNELS];
        var panEffect_counter : [u8 ; N_CHANNELS];
        var panEffect_offset_l : [u8 ; N_CHANNELS];
        var panEffect_offset_h : [u8 ; N_CHANNELS];
        var panEffect_halfWavelength : [u8 ; N_CHANNELS];


        // Offset between the note to play and the pitch table.
        //
        // NOTE: Adding `instPitchOffset` to the note to play is allowed to an can overflow.
        var instPitchOffset : [u8 ; N_CHANNELS];


        // The previous temp-GAIN used by the channel.
        var prevTempGain : [u8 ; N_CHANNELS];
    }
}


in lastvarpage {
    namespace channelSoA {
        // set_early_release instruction arguments
        var earlyRelease_cmp : [u8 ; N_CHANNELS];
        var earlyRelease_minTicks : [u8 ; N_CHANNELS];
        var earlyRelease_gain : [u8 ; N_CHANNELS];

        // Number of ticks since the last play_note or portamento instruction.
        // ::TODO move to zeropage::
        var ticksAfterNote : [u8 ; N_CHANNELS];
    }

    // The bytecode stack
    //
    // CAUTION: `bcStack` is offset by BC_CHANNEL_STACK_OFFSET to eliminate signed comparisons.
    var __bcStack : [u8 ; BC_CHANNEL_STACK_SIZE * N_CHANNELS];
        var bcStack @ (&__bcStack as u16) - BC_CHANNEL_STACK_OFFSET : [u8 ; BC_CHANNEL_STACK_SIZE * N_CHANNELS + BC_CHANNEL_STACK_OFFSET];
}


// Macros
// ======

// KEEP: a, x, y
inline func write_dsp(addr : GlobalDspAddr in a, data : u8 in y) {
    smp.dsp_addr_and_data = ya;
}


// ::BUGFIG This inline func fixes a "left-hand side of type `u16` cannot be assigned `u8` expression" error when multiplying a and y::
inline func mul(u8a : u8 in a, u8b : u8 in y) : u16 in ya {
    return a * y;
}



// Code
// ====

in code {

// MUST be the first thing in the `code` bank (so the starting address is consistent across builds).
func main() {
    sp = x = 0xff;
    direct_page = false;

    // Tell the S-CPU the S-SMP is running the audio driver
    IO.ToScpu.mode = IO.ToScpu.MODE_AUDIO_DRIVER;

    // Reset the DSP
    write_dsp(GlobalDspAddr.FLG,    dsp.FLG__SOFT_RESET | dsp.FLG__MUTE_ALL | dsp.FLG__ECHO_DISABLE);

    write_dsp(GlobalDspAddr.KOFF,   0xff);  // all keys off


    // Setup echo.
    //
    // This is done as soon as possible as we need to wait a while after setting EDL and ESA.
    {
        // Assumes ECHO write is disabled.

        y = offsetof(SongHeader, echo);

        // EchoBufferSettings.edl (mask for safety)
        a = songPtr[y] & 0xf;
        y++;

        // Set EDL
        smp.dsp_addr = GlobalDspAddr.EDL as u8;
        smp.dsp_data = a;


        // Calculate echo address

        // a = 0x100 - a * (2048 / 256)

        // Multiply by 8
        // (XCN is safe, A has been masked)
        swap_digits(a);
        a >>>= 1;

        // Negate A
        a ^= 0xff;
        a++;

        if zero {
            // Ensure echo buffer is at the end of audio-ram when EDL is 0
            // When EDL is 0, the echo buffer is 4 bytes in size.
            a--;
        }

        // set ESA (Echo start address??)
        // Assumes GlobalDspAddr.ESA | 0x10 = GlobalDspAddr.EDL
        smp.dsp_addr $ 4 = false;
        smp.dsp_data = a;

        // Save S-DSP ESA for later.
        push(a);


        // `EDL` does not take affect immediately.
        // The S-DSP reads `EDL` when the echo buffer is full and wraps around.
        //
        // Since `EDL` starts in an uninitialised state, we must wait a minimum of 240ms before
        // setting echo volume or enabling echo writes.
        //
        // Setup a timer here and enable echo writes at the end of driver initialization.
        //
        // Source: fullsnes https://problemkaputt.de/fullsnes.htm#snesapudspechoregisters

        smp.timer_0 = EDL_SLEEP_TIMER_0;

        // Reset and enable timer 0 (timers are reset on a transition from 0 to 1)
        smp.control = 0;
        smp.control = smp.CONTROL__ENABLE_TIMER_0;


        // Copy FIR filter from header to C0-C7
        smp.dsp_addr = GlobalDspAddr.C0 as u8;
        do {
            // EchoBufferSettings.firFilter
            smp.dsp_data = a = songPtr[y];
            y++;

            // Assumes GlobalDspAddr.C1 - GlobalDspAddr.C0 == 0x10
            // Assumes GlobalDspAddr.C7 + 0x10 >= 0x80
            smp.dsp_addr += 0x10;
        } while !negative;


        // EchoBufferSettings.echoFeedback
        a = songPtr[y];
        y++;

        smp.dsp_addr = GlobalDspAddr.EFB as u8;
        smp.dsp_data = a;


        // EchoBufferSettings.echoVolume
        a = songPtr[y];

        smp.dsp_addr = GlobalDspAddr.EVOL_L as u8;
        smp.dsp_data = a;

        // Assumes GlobalDspAddr.EVOL_R = GlobalDspAddr.EVOL_L | 0x10
        smp.dsp_addr $ 4 = true;
        smp.dsp_data = a;
    }

    // A copy of S-DSP `ESA` is on the stack.


    y = 0;
    write_dsp(GlobalDspAddr.PMON,   y);     // No pitch modulation
    write_dsp(GlobalDspAddr.NON,    y);     // No noise
    write_dsp(GlobalDspAddr.EON,    y);     // No echo


    // Set main volume to full volume
    y = 0x7f;
    write_dsp(GlobalDspAddr.MVOL_L, y);
    write_dsp(GlobalDspAddr.MVOL_R, y);

    // Set directory table
    write_dsp(GlobalDspAddr.DIR, >:COMMON_DATA_HEADER_END_ADDR);


    // Clear all zero-page variables before `songPtr`.
    // Assumes `songPtr` and `loaderDataType` is AFTER `zeropage` bank.
    a = 0;
    x = &songPtr as u8;
    do {
        x--;
        *(x as *u8) = a;
    } while !zero;


    a = 0xff;
    io_musicChannelsMask = a;
    musicSfxChannelMask = a;
    keyOnMask_music = a;
    noiseFreq_music = a;
    noiseFreq_sfx = a;

    inline for let _I in 0..(N_SFX_CHANNELS - 1) {
        sfx_oneChannelSfxId[_I] = a;
    }


    // Copy Common Audio Data pointers to zeropage
    {
        x = sizeof(typeof(commonData)) - 1;
        do {
            (&commonData as *u8)[x] = a = (&commonDataHeader.pointers as *u8)[x];
            x--;
        } while !negative;
    }


    // Calculate `subroutineTable` addresses.
    // subroutineTable_l = songPtr + sizeof(SongHeader)
    a = sizeof(SongHeader);
    y = 0;
    ya += songPtr as u16;
    subroutineTable_l = ya as *const u8;

    // subroutineTable_h = subroutineTable_l + nSubroutines
    y = offsetof(SongHeader, nSubroutines);
    a = songPtr[y];
    y = 0;
    ya += subroutineTable_l as u16;
    subroutineTable_h = ya as *const u8;


    x = N_MUSIC_CHANNELS - 1;
    do {
        // ASSUMES offsetof(SongHeader, channels) == 0
        // ASSUMES sizeof(SongHeader.channels[0]) == 2
        y = a = x <<< 1;

        // Calculate instructionPtr (disabling the channel if the addition overflows)
        channelSoA.instructionPtr_l[x] = a = songPtr[y] + <:songPtr;
        y++;
        a = songPtr[y] +# >:songPtr;
        if carry {
            // Disable channel
            a = 0;
        }
        channelSoA.instructionPtr_h[x] = a;

        __reset_channel(x, MAX_PAN / 2);

        a = STARTING_VOLUME >> 2;
        channelSoA.virtualChannels.vol_l[x] = a;
        channelSoA.virtualChannels.vol_r[x] = a;

        x--;
    } while !negative;


    // No need to reset SFX channels.
    // The sfx channels are disabled as `instructionPtr_h` is 0.


    // Pop ESA from the stack
    x = pop();


    // Skip echo buffer clear and `EDL` sleep if the SKIP_ECHO_BUFFER_RESET_BIT bit is set
    //
    // WARNING: Skipping the `EDL` sleep can corrupt memory when echo writes are enabled.
    //
    // (cannot use `bbs` instructions in wiz, using `mov1 c, addr, bit` instead)
    carry = loaderDataType $ LoaderDataType.SKIP_ECHO_BUFFER_RESET_BIT;
    if !carry {
        // Clear the echo buffer
        // X = S-DSP ESA
        __clear_echo_buffer__inline(x);


        // Wait until the echo buffer can be safely written to.
        // (wait until `EDL_SLEEP_COUNT` ticks of TIMER_0)
        a = EDL_SLEEP_COUNT - 1;
        do {
            a -= smp.counter_0;
        } while carry;
    }

    // Enable echo write and unmute the DSP
    write_dsp(GlobalDspAddr.FLG, 0);


    // Setup and enable the timers
    y = offsetof(SongHeader, tickTimer);
    smp.timer_0 = a = songPtr[y];

    smp.timer_1 = SFX_TICK_CLOCK;

    // Timers are enabled and reset in `io_commands.unpause()`


    io_commands.init__inline();


    // Reset counters to ensure `maxTimerCounter` is accurate
    // Timers and counters reset when the timer control bits transition from 0 to 1
    smp.control = 0;

    // (cannot use `bbs` instructions in wiz, using `mov1 c, addr, bit` instead)
    carry = loaderDataType $ LoaderDataType.PLAY_SONG_BIT;
    if carry {
        smp.control = smp.CONTROL__ENABLE_TIMER_0 | smp.CONTROL__ENABLE_TIMER_1;
    }

    ^while true {
        io_commands.process_io_ports__inline();

        // Song tick timer
        a = smp.counter_0;
        if !zero {
            if a >= maxTimerCounter {
                maxTimerCounter = a;
            }

            songTickCounter++;

            _process_music_channels__inline();
        }

        // SFX tick timer
        a = smp.counter_1;
        if !zero {
            if a >= maxTimerCounter {
                maxTimerCounter = a;
            }

            _process_sfx_channels__inline();
        }
    }
}



// Clears the echo buffer.
//
// ASSUMES: IPL is disabled
// ASSUMES: X matches the `ESA` S-DSP register
//
// WARNING: Can clobber the entirely of Audio-RAM if `esa` is invalid.
//
// WARNING: self modifying code
inline func __clear_echo_buffer__inline(esa : u8 in x) {
    // X = page address of the start of the echo buffer
    // Echo buffer ends at the end of memory.

    // Set the high byte of the `mov !abs+Y, a` instructions.
    (&STA_1 as *u8)[2] = x;
    (&STA_2 as *u8)[2] = x;

    a = 0;

    // Have to use a manual loop so the above code can access the `MOV_absY_A` label.
    OuterLoop:
        y = 0x80;
        InnerLoop:
            // Write 2 bytes every loop.
            // Writing 1 byte per loop is too slow (COUNTER_0 is 11 when it should be <=8 (TIMER_0=250)).
        STA_1:
            (0x0000 as *u8)[y] = a;
        STA_2:
            (0x0080 as *u8)[y] = a;

            goto InnerLoop if --y != 0;

        // Advance to the next page
        (&STA_1 as *u8)[2]++;
        (&STA_2 as *u8)[2]++;
        goto OuterLoop if !zero;
}


inline func _process_music_channels__inline() {
    y = a = keyOffShadow_music & musicSfxChannelMask;
    write_dsp(GlobalDspAddr.KOFF, y);

    y = noiseFreq_music;
    if !negative {
        // Assumes noiseFreq_music is <= dsp.FLG__NOISE_FREQ_MASK.
        write_dsp(GlobalDspAddr.FLG, y);
        noiseFreq_music $ 7 = true;
    }

    zpTmp = a = keyOnShadow_music & musicSfxChannelMask & io_musicChannelsMask;

    a = 0;
    // Music channel volume can only be modified by bytecode, never externally
    volShadowDirty_tmp = a;

    keyOnShadow_music = a;
    keyOffShadow_music = a;

    a = voiceChannelsDirty_music & musicSfxChannelMask;
    __process_channels(a, zpTmp, volShadowDirty_tmp, FIRST_MUSIC_CHANNEL + 1, LAST_MUSIC_CHANNEL + 1);

    voiceChannelsDirty_music = voiceChannelsDirty_tmp;
}


inline func _process_sfx_channels__inline() {
    // No need to mask sfx keyOff/keyOn events.
    // The sfx channels do not emit key-off or key-on events when disabled.

    // Delay echo and noise changes 1 tick to prevent noise/echo changing in
    // the middle of the release envelope the channel is ducked or un-ducked.
    sfxMusicEchoNoiseMask = prevActiveSoundEffects;

    prevActiveSoundEffects = a = activeSoundEffects;

    // Update `musicSfxChannelMask`.
    // Music channels can only write to the S-DSP when the sound effect is disabled AND there is no key-off event.
    // Prevents an audio glitch when a music channel writes to a voice channel in the middle of the key-off release envelope.
    musicSfxChannelMask = a = (a | keyOffShadow_sfx) ^ 0xff;

    write_dsp(GlobalDspAddr.KOFF, keyOffShadow_sfx);

    y = noiseFreq_sfx;
    if !negative {
        // Assumes noiseFreq_sfx is <= dsp.FLG__NOISE_FREQ_MASK.
        write_dsp(GlobalDspAddr.FLG, y);
        noiseFreq_sfx $ 7 = true;
    }

    zpTmp = keyOnShadow_sfx;
    volShadowDirty_tmp = volShadowDirty_sfx;

    a = 0;
    keyOnShadow_sfx = a;
    keyOffShadow_sfx = a;
    volShadowDirty_sfx = a;

    // DSP voice register writes must be masked
    a = voiceChannelsDirty_sfx & activeSoundEffects;
    __process_channels(a, zpTmp, volShadowDirty_tmp, FIRST_SFX_CHANNEL + 1, LAST_SFX_CHANNEL + 1);

    voiceChannelsDirty_sfx = voiceChannelsDirty_tmp;

    inline for let _I in 0..(N_SFX_CHANNELS - 1) {
        sfx_remainingTicks[_I]--;
    }
}


// Update the DSP voice registers, write to EON & KON, then execute channel bytecode.
//
// CAUTION: This function DOES NOT write to the `KOFF` S-DSP register.
func __process_channels(validChannels : u8 in a,
                        konShadow: u8 in zpTmp,
                        _volShadowDirty_tmp: u8 in volShadowDirty_tmp,
                        afterFirstChannel: u8 in zpTmp2,
                        afterLastChannel: u8 in x) {
var _konShadow : u8 in zpTmp;
var _afterFirstChannel : u8 in zpTmp2;

    channelIndexEndLoop = x;

    // This loop MUST read the channels (and voiceChannelsDirty_tmp) in the opposite order
    // to the loop below this one.
    // That way I can populate `_voiceChannelsDirty_tmp` using `ROR` and read it with `ASL`.
    voiceChannelsDirty_tmp = a;
    do {
        x--;

        voiceChannelsDirty_tmp <<<= 1;
        if carry {
            a = LastVoiceRegister[x];

            // Voice registers are written in reverse order so the ADSR2/GAIN voice register is
            // written BEFORE the ADSR1 voice register.
            //
            // This prevents a race-condition that can occur when changing the envelope from ADSR to
            // GAIN (or vice versa).  If the race-condition occurs, the previous ADSR2/GAIN value is
            // erroneously used for a single sample.
            //
            // This race condition can affect multiple samples if the previous GAIN was a fixed
            // value and thus audible to the listener.

            y = channelSoA.virtualChannels.tempGain[x];
            if !zero {
                smp.dsp_addr_and_data = ya;

                y = 0;
            }
            else {
                // Need to test which register (ADSR2 or GAIN) to write to.
                y = channelSoA.virtualChannels.adsr1[x];
                if negative {
                    a--;
                }
                y = channelSoA.virtualChannels.adsr2OrGain[x];
                smp.dsp_addr_and_data = ya;

                y = channelSoA.virtualChannels.adsr1[x];
            }

            // A = GAIN (0x?7) or ADSR2 (0x?6)
            a &= 0b1111_0110;
            // A = 0x?6
            a--;
            // A = ADSR1 (0x?5)
            smp.dsp_addr_and_data = ya;

            a--;
            y = channelSoA.virtualChannels.scrn[x];
            smp.dsp_addr_and_data = ya;

            a--;
            y = channelSoA.virtualChannels.pitch_h[x];
            smp.dsp_addr_and_data = ya;

            a--;
            y = channelSoA.virtualChannels.pitch_l[x];
            smp.dsp_addr_and_data = ya;

            a--;
            y = channelSoA.virtualChannels.vol_r[x];
            smp.dsp_addr_and_data = ya;

            a--;
            y = channelSoA.virtualChannels.vol_l[x];
            smp.dsp_addr_and_data = ya;
        }
    } while x >= _afterFirstChannel;


    y = a = ((eonShadow_music ^ eonShadow_sfx) & sfxMusicEchoNoiseMask) ^ eonShadow_music;
    write_dsp(GlobalDspAddr.EON, y);

    y = a = ((nonShadow_music ^ nonShadow_sfx) & sfxMusicEchoNoiseMask) ^ nonShadow_music;
    write_dsp(GlobalDspAddr.NON, y);


    write_dsp(GlobalDspAddr.KON, _konShadow);


    ^do {
        // MUST NOT use `_konShadow` and `_afterFirstChannel` in this loop.
        // The are clobbered by pitch effects and bytecode.


        voiceChannelsDirty_tmp >>>= 1;

        // Vibrato is processed before bytecode.
        // This delays all pitch changes 1 tick after a play-note instruction
        // and ensures the first pitch on a play-note instruction is the requested note.
        _process_vibrato__inline(x);

        // Must saturate-increment on every tick.
        // The note could be a part of a `play-note nokeyoff | rest` chain.
        a = channelSoA.ticksAfterNote[x];
        a++;
        if zero {
            a--;
        }
        channelSoA.ticksAfterNote[x] = a;

        channelSoA.countdownTimer[x]--;
        if !zero {
            // Check for early-release
            y = channelSoA.nextEventIsKeyOff[x];
            if !zero {
                // Do not early-release if the note has been playing for min-ticks
                if a >= channelSoA.earlyRelease_minTicks[x] {
                    // Must be a `<` comparison (not a `==`)
                    // Ensures notes shorter than early-release are cut at the correct time.
                    a = channelSoA.countdownTimer[x];
                    if a < channelSoA.earlyRelease_cmp[x] {
                        a = channelSoA.earlyRelease_gain[x];
                        if !zero {
                            channelSoA.virtualChannels.tempGain[x] = a;

                            voiceChannelsDirty_tmp $ 7 = true;
                        }
                        else {
                            // ::TODO deduplicate::
                            a = ChannelVoiceBit[x];
                            if x >= FIRST_SFX_CHANNEL {
                                test_and_set(a, keyOffShadow_sfx);
                                test_and_set(a, keyOnMask_sfx);
                            }
                            else {
                                test_and_set(a, keyOffShadow_music);
                                test_and_set(a, keyOnMask_music);
                            }
                        }
                    }
                }
            }
        }
        else {
            // Check if a KEY_OFF is required
            a = channelSoA.nextEventIsKeyOff[x];
            if !zero {
                a = ChannelVoiceBit[x];
                if x >= FIRST_SFX_CHANNEL {
                    test_and_set(a, keyOffShadow_sfx);
                    test_and_set(a, keyOnMask_sfx);
                }
                else {
                    test_and_set(a, keyOffShadow_music);
                    test_and_set(a, keyOnMask_music);
                }

                a = 0;
                // schedule a bytecode event
                channelSoA.nextEventIsKeyOff[x] = a;
                // Disable temp gain
                channelSoA.virtualChannels.tempGain[x] = a;

                // Previous countdownTimer value was 0
                channelSoA.countdownTimer[x]++;
            }
            else {
                y = channelSoA.instructionPtr_h[x];
                if !zero {
                    a = channelSoA.instructionPtr_l[x];
                    process_bytecode(x, ya);

                    ya = instructionPtr as u16;
                    channelSoA.instructionPtr_l[x] = a;
                    channelSoA.instructionPtr_h[x] = y;

                    // Request S-DSP voice register update (set dirty bit).
                    // The bytecode most likely changed the virtual channel.
                    //
                    // The flag is set here (and once) instead of every instruction that changes
                    // `channelSoA.virtualChannels` to save a lot of code space.
                    //
                    voiceChannelsDirty_tmp $ 7 = true;
                }
            }
        }

        a = channelSoA.volEffect_direction[x];
        if !zero {
            _process_volume_effects__inline(x, a);

            // ::TODO optimise::
            // Set S-DSP voice registers dirty bit
            voiceChannelsDirty_tmp $ 7 = true;

            volShadowDirty_tmp $ 7 = true;
        }

        a = channelSoA.panEffect_direction[x];
        if !zero {
            _process_pan_effects__inline(x, a);

            // ::TODO optimise::
            // Set S-DSP voice registers dirty bit
            voiceChannelsDirty_tmp $ 7 = true;

            volShadowDirty_tmp $ 7 = true;
        }


        // Cannot do this after process_bytecode() for 2 reasons:
        //  1. Volume must be calculated on the first instruction of a sound-effect
        //  2. Volume/pan slide/vibrato
        volShadowDirty_tmp <<<= 1;
        if carry {
            _update_vol_shadow__inline(x);
        }

        // Portamento is processed after bytecode to ensure pitch slide occurs
        // on the same tick as the portamento instruction.
        _process_portamento__inline(x);

        x++;
    } while x < channelIndexEndLoop;
}


// IO Ports
// ========

namespace io_commands {

inline func init__inline() {
    // Assumes audio driver starts paused.
    // Assumes IO.Command.PAUSE is 0
    // Assumes `IO.ToDriver.command` is non-zero from the loader.

    a = IO.Command.PAUSE as u8;
    IO.ToScpu.command_ack = a;
    previousCommand = a;
}


inline func process_io_ports__inline() {
    // If the S-CPU writes to an IO port at the same time S-SMP reads from the
    // same IO port, the S-SMP will read invalid data.
    //
    // To detect this bug the IO-Port is read twice and the command is only
    // processed if both reads are the same.
    //
    // Once the command byte has been read successfully, the two parameter bytes
    // do not need to be double-read because:
    //  * The S-CPU will wait until the command has been acknowledged before
    //    writing a new command
    //  * The parameter bytes are written before the command byte is written.

    // Test if `IO.command_id` has changed.
    a = IO.ToDriver.command;
    if a != previousCommand {
        if a == IO.ToDriver.command {
            // MUST set previousCommand before executing command
            // (`pause()` changes previousCommand)
            previousCommand = a;

            y = IO.ToDriver.parameter0;
            __call_command(a, y);

            // Acknowledge command
            IO.ToScpu.command_ack = previousCommand;
        }
    }


    // Execute the loader if the _SWITCH_TO_LOADER_BIT is set.
    //
    // (cannot use `bbc` instructions in wiz, using two `MOV1 ; bcc` instructions instead)
    carry = IO.ToDriver.switch_to_loader $ IO.ToDriver._SWITCH_TO_LOADER_BIT;
    if carry {
        // Read the IO port a second time just in case it was a glitch
        carry = IO.ToDriver.switch_to_loader $ IO.ToDriver._SWITCH_TO_LOADER_BIT;
        if carry {
            ^goto (LOADER_ADDR as func);
        }
    }
}


// NOTE: Does not acknowledge command
func __call_command(command : u8 in a, parameter : u8 in y) {
    x = a = a & IO.ToDriver.COMMAND_MASK;
    goto CommandFunctionTable[unaligned x];
}


// MUST match `Command` enum in `io-commands.wiz`
const CommandFunctionTable : [func(parameter0: u8 in y) ; IO.ToDriver.N_COMMANDS] = [
    pause as func(u8 in y),
    pause_music_play_sfx as func(u8 in y),
    unpause as func(u8 in y),
    play_sound_effect,
    stop_sound_effects,
    set_main_volume,
    set_music_channels,
    set_song_tempo,
];


// Pause music and sound effects
func pause() {
    return __write_control_and_keyoff(
        0,  // Disable timers
        0xff, // key-off all channels
    );
}


#[fallthrough]
func pause_music_play_sfx() {
    a = smp.CONTROL__ENABLE_TIMER_1; // Disable music timer, enable sfx timer
    y = musicSfxChannelMask; // key-off music channels

// fallthrough
}

func __write_control_and_keyoff(control : u8 in a, channelsMask: u8 in y) {
    smp.control = a;
    write_dsp(GlobalDspAddr.KOFF, y);
}


func unpause() {
    // Enable music and sfx timers
    smp.control = smp.CONTROL__ENABLE_TIMER_0 | smp.CONTROL__ENABLE_TIMER_1;
}


func set_main_volume(volume : u8 in y) {
    write_dsp(GlobalDspAddr.MVOL_L, y);
    write_dsp(GlobalDspAddr.MVOL_R, y);
}


func set_music_channels(mask : u8 in y) {
    io_musicChannelsMask = y;

    // key-off muted channels
    a = y ^ 0xff;
    test_and_set(a, keyOffShadow_music);
}


func set_song_tempo(tempo : u8 in y) {
    if y < MIN_TICK_CLOCK {
        y = MIN_TICK_CLOCK;
    }
    smp.timer_0 = y;
}


func stop_sound_effects(unused : u8 in y) {
    // Assumes 2 sound effect channels

    // `disable_channel` does not write to channelSoa.instructionPtr*.
    channelSoA.instructionPtr_h[FIRST_SFX_CHANNEL] = 0;
    channelSoA.instructionPtr_h[LAST_SFX_CHANNEL] = 0;

    bytecode.disable_channel(FIRST_SFX_CHANNEL);
    ^return bytecode.disable_channel(LAST_SFX_CHANNEL);
}


func play_sound_effect(sfx_id : u8 in y) {
    let BOTH_SFX_ACTIVE = (1 << 7) | (1 << 6);

    if y < commonDataHeader.nSoundEffects {
        goto _maybe_restart_sfx_0 if y == sfx_oneChannelSfxId[0];
        goto _maybe_restart_sfx_1 if y == sfx_oneChannelSfxId[1];

        a = activeSoundEffects;
        goto __play_sfx__both_channels_active if a >= BOTH_SFX_ACTIVE;

        // At most 1 sound effect channel is active
        a <<<= 1;
        goto _play_sfx_1 if !carry;
        goto _play_sfx_0;
    }
}


#[fallthrough]
func __play_sfx__both_channels_active(sfx_id : u8 in y) {
    let BOTH_SFX_ACTIVE = (1 << 7) | (1 << 6);

    // Both sound effect channels are active,

    // Drop sfx_id if it is a low-priority sound effect
    if y < commonDataHeader.firstLowPrioritySfx {
        a = sfx_interruptibleFlags;
        if (zero && y < commonDataHeader.nHighPrioritySfx) || a >= BOTH_SFX_ACTIVE {
            // Both sound effect channels active and either:
            //   * both uninterruptible and sfx_id is high-priority
            //   * both interruptible and sfx_id is normal priority
            //
            // check remaining ticks and drop the sound effect that will finish first.

            // Assumes 2 sound effect channels
            ya = sfx_remainingTicks[0];
            cmp(ya, sfx_remainingTicks[1]);

            y = IO.ToDriver.parameter0;
            goto _play_sfx_0 if !carry;
            goto _play_sfx_1;
        }

        // Play sound effect if a channel is interruptible
        a <<<= 1;
        goto _play_sfx_1 if carry;
        goto _play_sfx_0 if negative;
    }

// Fallthrough
}

func __play_sfx_return() {
    return;
}

// returns soundEffects_addrAndOneChannelFlag_h
inline func __play_sfx__inline(let _I : u8, sfx_id : u8 in y) : u8 in a {
    let BIT = 8 - N_SFX_CHANNELS + _I;

    <:sfx_remainingTicks[_I] = a = commonData.soundEffects_durationAndInterruptFlag_l[y];
    >:sfx_remainingTicks[_I] = a = commonData.soundEffects_durationAndInterruptFlag_h[y];

    a <<<= 1;
    sfx_interruptibleFlags $ BIT = carry;


    sfx_oneChannelSfxId[_I] = y;

    a = commonData.soundEffects_addrAndOneChannelFlag_h[y];
    if !negative {
        sfx_oneChannelSfxId[_I] = 0xff;
    }
    return a;
}


// Restart sfx channel 0 if it is interruptible
// ASSUMES: sfx_id == sfx_oneChannelSfxId[0]
#[fallthrough]
func _maybe_restart_sfx_0(sfx_id : u8 in y) {}
    // HACK: wiz does not support bbc instructions.
    // Manually writing a `bbc` instruction that branches to `__play_sfx_return` if the sfx is not interruptible.
    const __maybe_restart_sfx_0 : [u8 ; 3] = [
        // bbc sfx_interruptibleFlags.6 __play_sfx_return
        0x13 | (6 << 5) as u8,
        &sfx_interruptibleFlags as u8,
        (&__play_sfx_return as i16 - &__maybe_restart_sfx_0 as i16 - 3) as u8
    ];

func _play_sfx_0(sfx_id : u8 in y) {
    a = __play_sfx__inline(0, y);
    return __play_sfx_on_channel(a, FIRST_SFX_CHANNEL, y);
}



// Restart sfx channel 1 if it is interruptible
// ASSUMES: sfx_id == sfx_oneChannelSfxId[1]
#[fallthrough]
func _maybe_restart_sfx_1(sfx_id : u8 in y) {}
    // HACK: wiz does not support bbc instructions.
    // Manually writing a `bbc` instruction that branches to `__play_sfx_return` if the sfx is not interruptible.
    const __maybe_restart_sfx_1 : [u8 ; 3] = [
        // bbc sfx_interruptibleFlags.7 __play_sfx_return
        0x13 | (7 << 5) as u8,
        &sfx_interruptibleFlags as u8,
        (&__play_sfx_return as i16 - &__maybe_restart_sfx_1 as i16 - 3) as u8
    ];

#[fallthrough]
func _play_sfx_1(sfx_id : u8 in y) {
    a = __play_sfx__inline(1, y);
    x = LAST_SFX_CHANNEL;
// Fallthrough
}


// ASSUMES: sfx_id < commonData.nSoundEffects
// KEEP: X
#[fallthrough]
func __play_sfx_on_channel(addrAndOneChannelFlag_h: u8 in a, channelIndex : u8 in x, sfx_id : u8 in y) {
    channelSoA.instructionPtr_h[x] = a = a & 0x7f;
    channelSoA.instructionPtr_l[x] = a = commonData.soundEffects_addrAndOneChannelFlag_l[y];

    a = ChannelVoiceBit[x];
    // Send a key-off event, the S-DSP voice channel might be active.
    test_and_set(a, keyOffShadow_sfx);
    // Set keyOnMask.  The next play-note instruction will queue a KON event.
    test_and_set(a, keyOnMask_sfx);
    // Disable echo
    test_and_clear(a, eonShadow_sfx);

    // Not disabling noise.
    // Clearing nonShadow_sfx would disable noise in the middle of the release envelope.
    // Noise is disabled on `play_note` and `play_pitch`.

    // Enable sfx channel.
    test_and_set(a, activeSoundEffects);

    // Mark sfx channel volume dirty
    // eor used to swap bits 6 & 7 (since only 1 bit is set)
    a ^= 0b11_000000;
    test_and_set(a, volShadowDirty_sfx);


    // pan
    a = IO.ToDriver.parameter1;

// Fallthrough into __reset_channel()
}

}

// NOTE: Does not set:
//  * channelSoA.instructionPtr
//  * instrument/envelope
//  * virtual channels
//  * keyOnMask
//  * eonShadow
//  * nonShadow
//
// KEEP: X
func __reset_channel(channelIndex : u8 in x, pan: u8 in a) {
    channelSoA.pan[x] = a = pan;
    channelSoA.volume[x] = a = STARTING_VOLUME;


    a = 0;

    // Disable temp-gain
    channelSoA.virtualChannels.tempGain[x] = a;

    // Disable early-release
    channelSoA.earlyRelease_cmp[x] = a;

    // Disable portamento
    channelSoA.portamento_direction[x] = a as i8;

    // Disable vibrato
    channelSoA.vibrato_pitchOffsetPerTick[x] = a;

    // Disable volume effects
    channelSoA.volEffect_direction[x] = a;

    // Immediately process bytecode on the next tick
    channelSoA.nextEventIsKeyOff[x] = a; // a = 0
    a++;
    channelSoA.countdownTimer[x] = a;    // a = 1


    channelSoA.stackPointer[x] = a = BcStackIndexesStart[x];

    // Ensures loopStackPointer is in-bounds if the first instruction is `end_loop`.
    channelSoA.loopStackPointer[x] = a = a - 3;
}


// Volume and Pan
// ==============


// Process volume effect
//
// CAUTION: Does not set `volShadowDirty_tmp`.
//
// KEEP: x
inline func _process_volume_effects__inline(channelIndex: u8 in x, direction: u8 in a) {
    // Warning spaghetti code: Optimised for code-size.

    // Hack to allow access to volume effect variables with `direct_page = true`.
    var _csv_subVolume      @ &channelSoA.subVolume                 as u8 : [u8 ; N_CHANNELS];
    var _csv_direction      @ &channelSoA.volEffect_direction       as u8 : [u8 ; N_CHANNELS];
    var _csv_counter        @ &channelSoA.volEffect_counter         as u8 : [u8 ; N_CHANNELS];
    var _csv_offset_l       @ &channelSoA.volEffect_offset_l        as u8 : [u8 ; N_CHANNELS];
    var _csv_offset_h       @ &channelSoA.volEffect_offset_h        as u8 : [u8 ; N_CHANNELS];
    var _csv_halfWavelength @ &channelSoA.volEffect_halfWavelength  as u8 : [u8 ; N_CHANNELS];

    a >>>= 1;

    y = channelSoA.volume[x];

    direct_page = true;
    // MUST NOT access zeropage variables until direct_page is false.

    a = _csv_subVolume[x];

    goto Up if !carry;
        // down
        // carry set
        a -#= _csv_offset_l[x];
        _csv_subVolume[x] = a;

        a = y -# _csv_offset_h[x];

        goto EndIf if carry;
            y = 0;
            goto DisableEffect;

        UpOverflow:
            y = 0xff;
        DisableEffect:
            a = 0;
            goto WriteDirection;

    Up:
        // up
        // carry clear
        a +#= _csv_offset_l[x];
        _csv_subVolume[x] = a;

        a = y +# _csv_offset_h[x];

        goto UpOverflow if carry;
EndIf:

    y = a;

    _csv_counter[x]--;
    goto WriteVolume if !zero;

        a = _csv_halfWavelength[x];
        if !zero {
            _csv_counter[x] = a;

            // Change direction
            a = _csv_direction[x] ^ 1;
        }
    WriteDirection:
        _csv_direction[x] = a;

WriteVolume:
    direct_page = false;

    channelSoA.volume[x] = y;
}


// Process pan effect
//
// CAUTION: Does not set `volShadowDirty_tmp`.
//
// KEEP: x
inline func _process_pan_effects__inline(channelIndex: u8 in x, direction: u8 in a) {
    // Warning spaghetti code: Optimised for code-size.

    // Hack to allow access to pan effect variables with `direct_page = true`.
    var _csp_pan            @ &channelSoA.pan                       as u8 : [u8 ; N_CHANNELS];
    var _csp_subPan         @ &channelSoA.subPan                    as u8 : [u8 ; N_CHANNELS];
    var _csp_direction      @ &channelSoA.panEffect_direction       as u8 : [u8 ; N_CHANNELS];
    var _csp_counter        @ &channelSoA.panEffect_counter         as u8 : [u8 ; N_CHANNELS];
    var _csp_offset_l       @ &channelSoA.panEffect_offset_l        as u8 : [u8 ; N_CHANNELS];
    var _csp_offset_h       @ &channelSoA.panEffect_offset_h        as u8 : [u8 ; N_CHANNELS];
    var _csp_halfWavelength @ &channelSoA.panEffect_halfWavelength  as u8 : [u8 ; N_CHANNELS];

    direct_page = true;
    // MUST NOT access zeropage variables until direct_page is false.

    a >>>= 1;

    y = _csp_pan[x];

    a = _csp_subPan[x];

    goto Up if !carry;
        // down
        // carry set
        a -#= _csp_offset_l[x];
        _csp_subPan[x] = a;

        a = y -# _csp_offset_h[x];

        goto EndIf if carry;
            y = 0;
            goto DisableEffect;

        UpOverflow:
            y = MAX_PAN;
        DisableEffect:
            a = 0;
            goto WriteDirection;

    Up:
        // up
        // carry clear
        a +#= _csp_offset_l[x];
        _csp_subPan[x] = a;

        a = y +# _csp_offset_h[x];

        goto UpOverflow if carry || a >= (MAX_PAN + 1);
EndIf:

    y = a;

    _csp_counter[x]--;
    goto WritePan if !zero;

        a = _csp_halfWavelength[x];
        if !zero {
            _csp_counter[x] = a;

            // Change direction
            a = _csp_direction[x] ^ 1;
        }
    WriteDirection:
        _csp_direction[x] = a;

WritePan:
    _csp_pan[x] = y;

    direct_page = false;
}


// Updates vol_l and vol_r shadow variables.
//
// NOTE: Does not change `channelSoA.virtualChannels.updateState[x]`.
//
// KEEP: x
inline func _update_vol_shadow__inline(channelIndex : u8 in x) {

    // Reading carry here saves a `SEC` instruction when calculating VOL_R
    carry = loaderDataType $ LoaderDataType.STEREO_FLAG_BIT;
    if !carry {
        // Mono

        // First shift converts 8-bit volume to 7-bit VOL
        // Second shift ensure mono vol has same combined amplitude as stereo vol
        a = channelSoA.volume[x] >>> 2;

        channelSoA.virtualChannels.vol_l[x] = a;
        channelSoA.virtualChannels.vol_r[x] = a;
    }
    else {
        // Stereo
        // carry set

        // Calculate right volume
        // A pan value of `MAX_PAN` is 100% to the right.
        // (MUST NOT USE CARRY)

        a = channelSoA.pan[x];
        y = channelSoA.volume[x];
        ya = mul(a, y);
        channelSoA.virtualChannels.vol_r[x] = y;


        // Calculate left volume
        // A pan value of `0` is 100% to the left.

        // Carry set (MUL does not set carry)
        a = MAX_PAN -# channelSoA.pan[x];
        y = channelSoA.volume[x];
        ya = mul(a, y);
        channelSoA.virtualChannels.vol_l[x] = y;
    }
}



// Pitch Effects
// =============


// KEEP: X
inline func _process_vibrato__inline(channelIndex : u8 in x) {

    // (vibrato is not active during portamento)
    y = channelSoA.portamento_direction[x] as u8;
    if zero {
        y = channelSoA.vibrato_pitchOffsetPerTick[x];
        if !zero {
            // Determine the vibrato direction
            a = channelSoA.vibrato_directionComparator[x];
            cmp(a, channelSoA.vibrato_tickCounter[x]);

            if !carry {
                // Add Y to pitch_l
                // carry clear
                a = y +# channelSoA.virtualChannels.pitch_l[x];
                if carry {
                    channelSoA.virtualChannels.pitch_h[x]++;
                }
            }
            else {
                // Subtract pitchOffsetPerTick from pitch_l
                // (uses 1 less byte than reverse subtract Y, with the same CPU cycle count)
                // carry set
                a = channelSoA.virtualChannels.pitch_l[x] -# channelSoA.vibrato_pitchOffsetPerTick[x];
                if !carry {
                    channelSoA.virtualChannels.pitch_h[x]--;
                }
            }

            channelSoA.virtualChannels.pitch_l[x] = a;

            channelSoA.vibrato_tickCounter[x]--;
            if zero {
                channelSoA.vibrato_tickCounter[x] = a = channelSoA.vibrato_waveLengthInTicks[x];
            }

            // Set S-DSP voice registers dirty bit
            voiceChannelsDirty_tmp $ 7 = true;
        }
    }
}


// KEEP: X
inline func _process_portamento__inline(channelIndex : u8 in x) {
var _target_h : u8 in zpTmp;

    // Warning: spaghetti code

    a = channelSoA.portamento_direction[x] as u8;
    if !zero {
        // Portamento

        if negative {
            // portamento down

            // Save target_h for later (no `cpy dp,x` instruction)
            _target_h = a = channelSoA.portamento_target_h[x];

            // Update pitch
            a = channelSoA.virtualChannels.pitch_l[x];
            y = channelSoA.virtualChannels.pitch_h[x];

            // Subtract speed from pitch
            a -= channelSoA.portamento_speed[x];
            if !carry {
                y--;
                // Test for underflow
                goto MetTarget if negative;
            }

            // Test pitch against target
            cmp(y, _target_h);
            if zero {
                cmp(a, channelSoA.portamento_target_l[x]);
            }
            goto WritePitchRegisters if carry;
        }
        else {
            // portamento up

            // Save target_h for later (no `cpy dp,x` instruction)
            _target_h = a = channelSoA.portamento_target_h[x];

            a = channelSoA.virtualChannels.pitch_l[x];
            y = channelSoA.virtualChannels.pitch_h[x];

            // Add speed to pitch
            a += channelSoA.portamento_speed[x] as u8;
            if carry {
                y++;
                // PITCH is 14 bits long, no overflow test required.
            }

            // Test pitch against target
            cmp(y, _target_h);
            if zero {
                cmp(a, channelSoA.portamento_target_l[x]);
            }
            goto WritePitchRegisters if !carry;
        }

        // Pitch reached or exceeded target
    MetTarget:
        // Disable portamento
        a = 0;
        channelSoA.portamento_direction[x] = a as i8;

        a = channelSoA.portamento_target_l[x];
        y = _target_h;

    WritePitchRegisters:
        // ya = new pitch
        channelSoA.virtualChannels.pitch_l[x] = a;
        channelSoA.virtualChannels.pitch_h[x] = y;

        // Set S-DSP voice registers dirty bit
        voiceChannelsDirty_tmp $ 7 = true;
    }
}


// Process Bytecode
// ================
//
// bytecode functions MUST NOT modify X.
//
// bytecode functions MUST NOT modify S-DSP registers.


// See `process_bytecode_with_loader_test()`.
func __bbs_loader_bit_set(channelIndex : u8 in x, addr : u16 in ya) {
    // Test the SWITCH_TO_LOADER_BIT a second time (just to be safe) before switching to the loader

    // (cannot use `bbc` instructions in wiz, using an `MOV1 ; bcc` instruction instead)
    carry = IO.ToDriver.switch_to_loader $ IO.ToDriver._SWITCH_TO_LOADER_BIT;
    goto process_bytecode if !carry;

    ^goto (LOADER_ADDR as func);
}


// Set the bytecode `instructionPtr` and execute the next bytecode instruction while also
// testing the if `_SWITCH_TO_LOADER_BIT` bit is set.
//
// If the `_SWITCH_TO_LOADER_BIT` bit is set, the audio driver stops execution and the loader is
// started.  This should hopefully ensure the loader will be executed if the bytecode goes off the
// rails and never sleeps.
//
// KEEP: X
//
#[fallthrough]
func process_bytecode_with_loader_test(channelIndex : u8 in x, addr : u16 in ya) {
}

// HACK: wiz does not support bbs instructions.
// Manually writing a `bbs` instruction that branches to `__bbs_loader_bit_set` if the
// _SWITCH_TO_LOADER_BIT is set. Otherwise, falls through into the `process_bytecode` instruction.
const __process_bytecode_with_loader_test : [u8 ; 3] = [
    0x3 | (IO.ToDriver._SWITCH_TO_LOADER_BIT << 5) as u8,                                   // bbs
    &IO.ToDriver.switch_to_loader as u8,                                                    // addr
    (&__bbs_loader_bit_set as i16 - &__process_bytecode_with_loader_test as i16 - 3) as u8  // rel
];



// Set the bytecode `instructionPtr` and execute the next bytecode instruction.
// KEEP: X
#[fallthrough]
func process_bytecode(channelIndex : u8 in x, addr : u16 in ya) {
    instructionPtr = ya as *const u8;

// Fallthrough
}


// Execute the next bytecode instruction
//
// KEEP: X
func process_next_bytecode(channelIndex : u8 in x) {

    y = 0;
    a = instructionPtr[y];
    instructionPtr++;

    goto bytecode._play_note if a >= bytecode.InstructionTable.len;

    // A is a non play-note bytecode
    y = a = a <<<= 1;

    // Push return address to the stack.
    //
    // This method uses the same number of CPU cycles (and the least amount of code space) compared to:
    //   * `JMP[!abs+X]` plus saving/restoring X via a zeropage register (as there is no `TXY` or `TXY` instruction
    //   * self modifying code (writing the return address of a JMP instruction)
    //
    push(a = (&>:bytecode.InstructionTable[0] as *u8)[y]);
    push(a = (&<:bytecode.InstructionTable[0] as *u8)[y]);

    if y < bytecode.FIRST_NO_ARGUMENT_INSTRUCTION_OPCODE * 2 {
        // Instruction has a parameter
        y = 0;
        a = instructionPtr[y];
        instructionPtr++;
    }

    // Y MUST be 0 if the instruction has an argument

    // Jump to the bytecode instruction routine that was just pushed onto the stack.
    return;
}



// NOTE: BYTECODE INSTRUCTIONS MUST KEEP X
//
// NOTE: If the instruction has an argument, Y will be 0
//
// Bytecode instruction must either:
//    * Return after setting `channelSoA.nextEvent_*` (to start sleeping)
//    * Jump to `process_next_bytecode` to execute the next bytecode instruction
//    * Jump to `process_bytecode` to change the bytecode instruction pointer and execute it.
//    * Jump to `process_bytecode_with_loader_test`.
//
namespace bytecode {


// ASSUMES: in the channel process loop
// KEEP: x
inline func mark_vol_shadow_dirty__inline() {
    volShadowDirty_tmp $ 7 = true;
}



// KEEP: X
func skip_last_loop(channelIndex : u8 in x, bytesToSkip : u8 in a) {
    // `skip_last_loop` is here so I can `bne` to process_bytecode().
    return __skip_last_loop__inline(x, a);
}



// IN: Y = 0
// KEEP: X
#[fallthrough]
func set_vibrato_depth_and_play_note(channelIndex : u8 in x, pitchOffsetPerTick: u8 in a) {
    channelSoA.vibrato_pitchOffsetPerTick[x] = a;

    // Read first byte of `_play_note`
    // Y = 0
    a = instructionPtr[y];
    instructionPtr++;

// Fallthrough
}


// Y unknown
// KEEP: x
#[fallthrough]
func _play_note(channelIndex : u8 in x, noteAndKeyOffBit : u8 in a) {
    // carry = key-off after note ends bit
    a >>>= 1;

    // Set MSB of nextEventIsKeyOff from carry
    channelSoA.nextEventIsKeyOff[x] >>>>#= 1;

    // calculate pitch table index
    y = a = a + channelSoA.instPitchOffset[x];

    // Set voice pitch
    channelSoA.virtualChannels.pitch_l[x] = a = commonData.pitchTable_l[y];
    a = commonData.pitchTable_h[y];

// Fallthough into __play_note_set_pitch_h();
}


// Y unknown
// KEEP: X
func __play_note_set_pitch_h(channelIndex: u8 in x, pitch_h: u8 in a) {
    channelSoA.virtualChannels.pitch_h[x] = a;

    // Get note length from the next byte in the instructionPtr
    y = 0;
    a = instructionPtr[y];
    instructionPtr++;

    channelSoA.countdownTimer[x] = a;

    // Y = 0
    // disable portamento
    channelSoA.portamento_direction[x] = y as i8;

    // Reset early release min-tick counter.
    // Y = 0
    a = y;
    channelSoA.ticksAfterNote[x] = a;

    // Restart vibrato
    channelSoA.vibrato_tickCounter[x] = a = channelSoA.vibrato_tickCounterStart[x];


    // queue KON if the previous note has been KOFFed
    a = ChannelVoiceBit[x];
    if x >= FIRST_SFX_CHANNEL {
        // Disable noise
        test_and_clear(a, nonShadow_sfx);

        a &= keyOnMask_sfx;
        test_and_set(a, keyOnShadow_sfx);

        // Disable KON the next time the note is played (slur the note)
        test_and_clear(a, keyOnMask_sfx);

        return;
    }

    // Disable noise
    test_and_clear(a, nonShadow_music);

    a &= keyOnMask_music;
    test_and_set(a, keyOnShadow_music);

    // Disable KON the next time the note is played (slur the note)
    test_and_clear(a, keyOnMask_music);
}


// IN: Y = 0
// KEEP: X
func play_pitch(channelIndex: u8 in x, pitch_l : u8 in a) {
    channelSoA.virtualChannels.pitch_l[x] = a;

    // Y = 0
    a = instructionPtr[y];  // pitch_h and key-off flag
    instructionPtr++;

    a >>>= 1;
    // Set MSB of nextEventIsKeyOff from carry
    channelSoA.nextEventIsKeyOff[x] >>>>#= 1;

    return __play_note_set_pitch_h(x, a);
}


// IN: Y = 0
// KEEP: X
func play_noise(channelIndex: u8 in x, freq_and_keyoff : u8 in a) {
    a >>>= 1;
    // Set MSB of nextEventIsKeyOff from carry
    channelSoA.nextEventIsKeyOff[x] >>>>#= 1;

    a &= dsp.FLG__NOISE_FREQ_MASK;
    push(a);

    // Wait length
    // Y = 0
    a = instructionPtr[y];
    instructionPtr++;

    channelSoA.countdownTimer[x] = a;

    // Reset early release min-tick counter.
    // Y = 0
    a = y;
    channelSoA.ticksAfterNote[x] = a;

    y = pop();

    // queue KON if the previous note has been KOFFed
    a = ChannelVoiceBit[x];
    if x >= FIRST_SFX_CHANNEL {
        noiseFreq_sfx = y;

        a = ChannelVoiceBit[x];
        test_and_set(a, nonShadow_sfx);


        a &= keyOnMask_sfx;
        test_and_set(a, keyOnShadow_sfx);

        // Disable KON the next time the note is played (slur the note)
        test_and_clear(a, keyOnMask_sfx);

        return;
    }

    noiseFreq_music = y;

    test_and_set(a, nonShadow_music);


    a &= keyOnMask_music;
    test_and_set(a, keyOnShadow_music);

    // Disable KON the next time the note is played (slur the note)
    test_and_clear(a, keyOnMask_music);
}


// KEEP: X
func disable_noise(channelIndex: u8 in x) {
    a = ChannelVoiceBit[x];
    if x >= FIRST_SFX_CHANNEL {
        test_and_clear(a, nonShadow_sfx);

        ^return process_next_bytecode(x);
    }

    test_and_clear(a, nonShadow_music);

    ^return process_next_bytecode(x);
}


// IN: Y = 0
// KEEP: X
func portamento_down(channelIndex : u8 in x, speed : u8 in a) {
    channelSoA.portamento_speed[x] = a;

    return __portamento(channelIndex, -1 as u8);
}



// IN: Y = 0
// KEEP: X
#[fallthrough]
func portamento_up(channelIndex : u8 in x, speed : u8 in a) {
    channelSoA.portamento_speed[x] = a;

    // direction
    a = 1;

// Fallthrough
}


// Assumes portamento_speed already set
//
// IN: Y = 0
// KEEP: X
func __portamento(channelIndex : u8 in x, direction : u8 in a) {
    channelSoA.portamento_direction[x] = a as i8;

    // Wait length
    // Y = 0
    a = instructionPtr[y];
    instructionPtr++;

    channelSoA.countdownTimer[x] = a;

    // Reset early release min-tick counter.
    // Y = 0
    a = y;
    channelSoA.ticksAfterNote[x] = a;

    // Read note target and key-off bit
    // Y = 0
    a = instructionPtr[y];
    instructionPtr++;

    // carry = key-off after note ends bit
    a >>>= 1;
    // Set MSB of nextEventIsKeyOff from carry
    channelSoA.nextEventIsKeyOff[x] >>>>#= 1;

    // calculate pitch table index
    y = a = a + channelSoA.instPitchOffset[x];

    channelSoA.portamento_target_l[x] = a = commonData.pitchTable_l[y];
    channelSoA.portamento_target_h[x] = a = commonData.pitchTable_h[y];


    // return (do not execute the next bytecode and sleep)
    return;
}



// IN: Y = 0
// KEEP: X
func set_vibrato(channelIndex : u8 in x, pitchOffsetPerTick: u8 in a) {
    channelSoA.vibrato_pitchOffsetPerTick[x] = a;


    // Read quarterWavelengthInTicks parameter
    // Y = 0
    a = instructionPtr[y];
    instructionPtr++;

    channelSoA.vibrato_tickCounterStart[x] = a;
    channelSoA.vibrato_tickCounter[x] = a;

    a <<<= 1;
    channelSoA.vibrato_directionComparator[x] = a;

    a <<<= 1;
    channelSoA.vibrato_waveLengthInTicks[x] = a;

    ^return process_next_bytecode(x);
}



// KEEP: X
func start_loop(channelIndex : u8 in x, loopCount : u8 in a) {
    var _loopCount : u8 in  zpTmp;

    _loopCount = a;

    a = channelSoA.stackPointer[x] - 3;
    goto disable_channel if a < BcStackIndexesEnd[x];

    channelSoA.stackPointer[x] = a;
    channelSoA.loopStackPointer[x] = a;

    y = a;
    (&bcStack[0])[y] = a = _loopCount;
    (&bcStack[1])[y] = a = <:instructionPtr;
    (&bcStack[2])[y] = a = >:instructionPtr;

    ^return process_next_bytecode(x);
}



// KEEP: X
inline func __skip_last_loop__inline(channelIndex : u8 in x, bytesToSkip : u8 in a) {
    var _bytesToSkip : u8 in  zpTmp;

    _bytesToSkip = a;

    // No need to bounds check `loopStackPointer`, it is always in bounds.
    y = channelSoA.loopStackPointer[x];

    a = (&bcStack[0])[y]; // loop counter
    a--;
    goto process_next_bytecode if !zero;

    // Assumes `y + 3` is a valid stackPointer
    channelSoA.stackPointer[x] = a = y + 3;
    if a < ValidLoopStackPointerTable[x] {
        // Only write to loopStackPointer if it is valid
        channelSoA.loopStackPointer[x] = a;
    }

    a = _bytesToSkip;
    y = 0;
    ya += instructionPtr as u16;

    ^return process_bytecode_with_loader_test(x, ya);
}



// KEEP: X
func end_loop(channelIndex : u8 in x) {
    // No need to bounds check `loopStackPointer`, it is always in bounds.
    y = channelSoA.loopStackPointer[x];

    a = (&bcStack[0])[y]; // loop counter
    a--;
    if !zero {
        (&bcStack[0])[y] = a; // loop counter

        <:instructionPtr = a = (&bcStack[1])[y];
        >:instructionPtr = a = (&bcStack[2])[y];

        ^return process_next_bytecode(x);
    }

    // Assumes `y + 3` is a valid stackPointer
    channelSoA.stackPointer[x] = a = y + 3;
    if a < ValidLoopStackPointerTable[x] {
        // Only write to loopStackPointer if it is valid
        channelSoA.loopStackPointer[x] = a;
    }

    ^return process_next_bytecode(x);
}



// KEEP: X
func disable_channel(channelIndex : u8 in x) {
    // Y may not be 0

    // countdownTimer value of 0 means wait 256 ticks
    a = 0;
    channelSoA.countdownTimer[x] = a;

    // No bytecode is processed if instructionPtr_h is 0
    >:instructionPtr = a;

    // Disable portamento
    channelSoA.portamento_direction[x] = a as i8;

    // Disable vibrato
    channelSoA.vibrato_pitchOffsetPerTick[x] = a;


    // Determine if this is a music or sound effect channel.
    a = ChannelVoiceBit[x];
    if x >= FIRST_SFX_CHANNEL {
        // X is a sound effect channel

        // Send key-off event
        test_and_set(a, keyOffShadow_sfx);

        // Disable echo
        test_and_clear(a, eonShadow_sfx);

        // Not disabling noise.
        // Clearing nonShadow_sfx would disable noise in the middle of the release envelope.

        // Disable sfx S-DSP writes.
        test_and_clear(a, activeSoundEffects);

        // Clear `sfx_oneChannelSfxId` so the sfx can be restarted if it is not interruptible.
        (&sfx_oneChannelSfxId[-N_MUSIC_CHANNELS])[x] = a = 0xff;

        // return (do not execute the next bytecode and sleep)
        return;
    }


    // X is a music channel

    // Send key-off event
    test_and_set(a, keyOffShadow_music);

    // return (do not execute the next bytecode and sleep)
    return;
}



// Y = 0
// KEEP: X
func goto_relative(channelIndex : u8 in x, offset_l : u8 in a) {
    // Y = 0
    push(a);
    y = a = instructionPtr[y];
    a = pop();

    ya += instructionPtr as u16;
    ^return process_bytecode_with_loader_test(x, ya);
}



// IN: Y = 0
// KEEP: X
#[fallthrough]
func call_subroutine_and_disable_vibrato(channelIndex : u8 in x, subroutineId : u8 in a) {
    // Disable vibrato
    // Y = 0
    channelSoA.vibrato_pitchOffsetPerTick[x] = y;

// Fallthrough
}


// IN: Y = 0
// KEEP: X
func call_subroutine(channelIndex : u8 in x, subroutineId : u8 in a) {
var _subroutineId : u8 in zpTmp;

    _subroutineId = a;

    a = channelSoA.stackPointer[x];
    a--;
    a--;
    goto disable_channel if a < BcStackIndexesEnd[x];

    channelSoA.stackPointer[x] = a;

    y = a;
    (&bcStack[0])[y] = a = <:instructionPtr;
    (&bcStack[1])[y] = a = >:instructionPtr;

    // Add songPtr to subroutineTable[y] to get the subroutine address
    y = _subroutineId;
    <:instructionPtr = a = (subroutineTable_l as *u8)[y] + <:songPtr;
    >:instructionPtr = a = (subroutineTable_h as *u8)[y] +# >:songPtr;

    ^return process_next_bytecode(x);
}



// KEEP: X
#[fallthrough]
func return_from_subroutine_and_disable_vibrato(channelIndex : u8 in x) {
    // Disable vibrato
    channelSoA.vibrato_pitchOffsetPerTick[x] = a = 0;

// Fallthrough
}


// KEEP: X
func return_from_subroutine(channelIndex : u8 in x) {

    a = channelSoA.stackPointer[x];
    a++;
    goto disable_channel if a >= BcStackIndexesStart[x];
    a++;

    channelSoA.stackPointer[x] = a;

    if a < ValidLoopStackPointerTable[x] {
        // Only write to loopStackPointer if it is valid
        channelSoA.loopStackPointer[x] = a;
    }

    y = a;
    <:instructionPtr = a = (&bcStack[-2])[y];
    >:instructionPtr = a = (&bcStack[-1])[y];

    ^return process_next_bytecode(x);
}



// KEEP: X
func enable_echo(channelIndex : u8 in x) {
    a = ChannelVoiceBit[x];
    if x >= FIRST_SFX_CHANNEL {
        test_and_set(a, eonShadow_sfx);
        ^return process_next_bytecode(x);
    }

    test_and_set(a, eonShadow_music);
    ^return process_next_bytecode(x);
}



// KEEP: X
func disable_echo(channelIndex : u8 in x) {
    a = ChannelVoiceBit[x];
    if x >= FIRST_SFX_CHANNEL {
        test_and_clear(a, eonShadow_sfx);
        ^return process_next_bytecode(x);
    }

    test_and_clear(a, eonShadow_music);
    ^return process_next_bytecode(x);
}



// IN: Y = 0
// KEEP: X
func set_instrument(channelIndex : u8 in x, instrumentId : u8 in a) {
    // disable temp GAIN
    channelSoA.virtualChannels.tempGain[x] = y;

    y = a;

    channelSoA.instPitchOffset[x] = a = commonData.instruments_pitchOffset[y];

    channelSoA.virtualChannels.scrn[x] = a = commonData.instruments_scrn[y];
    channelSoA.virtualChannels.adsr1[x] = a = commonData.instruments_adsr1[y];
    channelSoA.virtualChannels.adsr2OrGain[x] = a = commonData.instruments_adsr2OrGain[y];

    ^return process_next_bytecode(x);
}



// IN: Y = 0
// KEEP: X
func set_instrument_and_adsr_or_gain(channelIndex : u8 in x, instrumentId : u8 in a) {
    // disable temp GAIN
    channelSoA.virtualChannels.tempGain[x] = y;

    y = a;

    channelSoA.instPitchOffset[x] = a = commonData.instruments_pitchOffset[y];

    channelSoA.virtualChannels.scrn[x] = a = commonData.instruments_scrn[y];

    // Read ADSR/GAIN from `instructionPtr`.
    y = 0;
    channelSoA.virtualChannels.adsr1[x] = a = instructionPtr[y];
    instructionPtr++;

    channelSoA.virtualChannels.adsr2OrGain[x] = a = instructionPtr[y];
    instructionPtr++;

    ^return process_next_bytecode(x);
}



// IN: Y = 0
// KEEP: X
func set_adsr(channelIndex: u8 in x, adsr1 : u8 in a) {
    // disable temp GAIN
    channelSoA.virtualChannels.tempGain[x] = y;

    channelSoA.virtualChannels.adsr1[x] = a;

    // Y = 0
    channelSoA.virtualChannels.adsr2OrGain[x] = a = instructionPtr[y];
    instructionPtr++;

    ^return process_next_bytecode(x);
}



// IN: Y = 0
// KEEP: X
func set_gain(channelIndex: u8 in x, gain : u8 in a) {
    // disable temp GAIN
    channelSoA.virtualChannels.tempGain[x] = y;

    // Y = 0
    channelSoA.virtualChannels.adsr1[x] = y;

    channelSoA.virtualChannels.adsr2OrGain[x] = a;

    ^return process_next_bytecode(x);
}



// Y = 0
// KEEP: X
func set_early_release_no_minimum(channelIndex: u8 in x, cmp_: u8 in a) {
    channelSoA.earlyRelease_cmp[x] = a;

    // Y = 0
    a = y;
    channelSoA.earlyRelease_minTicks[x] = a;

    return __set_early_release_gain(x);
}


// Y = 0
// KEEP: X
#[fallthrough]
func set_early_release(channelIndex: u8 in x, cmp_: u8 in a) {
    channelSoA.earlyRelease_cmp[x] = a;

    // Y = 0
    a = instructionPtr[y];
    instructionPtr++;
    channelSoA.earlyRelease_minTicks[x] = a;

// Fallthrough into __set_early_release_gain(x);
}


// Y = 0
// KEEP: X
func __set_early_release_gain(channelIndex: u8 in x) {
    // Y = 0
    a = instructionPtr[y];
    instructionPtr++;

    channelSoA.earlyRelease_gain[x] = a;

    ^return process_next_bytecode(x);
}


// KEEP: X
#[fallthrough]
func adjust_pan(channelIndex : u8 in x, detla : i8 in a) {
    carry = false;

    y = a;
    if negative {
        // carry clear, A is negative
        a +#= channelSoA.pan[x];
        if !carry {
            a = 0;
        }
    }
    else {
        // carry clear, A is positive
        a += channelSoA.pan[x];
        if carry || a >= MAX_PAN + 1 {
            a = MAX_PAN;
        }
    }

// Fallthrough
}



// KEEP: X
func set_pan(channelIndex : u8 in x, pan : u8 in a) {
    channelSoA.pan[x] = a;

    // Disable pan effects
    // (Y is non-zero)
    channelSoA.panEffect_direction[x] = a = 0;

    mark_vol_shadow_dirty__inline();

    ^return process_next_bytecode(x);
}



// REQUIRES: Y = 0
// KEEP: X
func set_pan_and_volume(channelIndex : u8 in x, pan : u8 in a) {
    // Y is 0

    channelSoA.pan[x] = a;

    // Disable pan effects
    // Y = 0
    channelSoA.panEffect_direction[x] = a = y;

    a = instructionPtr[y];
    instructionPtr++;

    return set_volume(x, a);
}



// KEEP:X
#[fallthrough]
func adjust_volume(channelIndex : u8 in x, delta: i8 in a) {
    carry = false;

    y = a;
    if negative {
        // carry clear, A is negative
        a +#= channelSoA.volume[x];
        if !carry {
            a = 0;
        }
    }
    else {
        // carry clear, A is positive
        a +#= channelSoA.volume[x];
        if carry {
            a = 0xff;
        }
    }

// Fallthrough
}


// KEEP: X
func set_volume(channelIndex : u8 in x, volume : u8 in a) {
    channelSoA.volume[x] = a;

    mark_vol_shadow_dirty__inline();

    // Disable volume effects
    // (Y is non-zero)
    channelSoA.volEffect_direction[x] = a = 0;

    ^return process_next_bytecode(x);
}


// Y = 0
// KEEP: x
func volume_slide_up(channelIndex: u8 in x, ticks: u8 in a) {
    var _csv_counter        @ &channelSoA.volEffect_counter         as u8 : [u8 ; N_CHANNELS];

    direct_page = true;

    _csv_counter[x] = a;

    // Y (sub_volume) = 0
    return __volume_slide__dp_true__(x, VOL_PAN_EFFECT_SLIDE_UP, y);
}


// Y = 0
// KEEP: x
#[fallthrough]
func volume_slide_down(channelIndex: u8 in x, ticks: u8 in a) {
    var _csv_counter        @ &channelSoA.volEffect_counter         as u8 : [u8 ; N_CHANNELS];

    direct_page = true;

    _csv_counter[x] = a;

    a = VOL_PAN_EFFECT_SLIDE_DOWN;

    y--;
    // Y (sub_volume) = 0xff

// Fallthrough into __volume_slide__dp_true__()
}


// Y = 0
// direct_page = true
// KEEP: x
#[fallthrough]
func __volume_slide__dp_true__(channelIndex: u8 in x, direction: u8 in a, subVolume: u8 in y) {
    var _csv_direction      @ &channelSoA.volEffect_direction       as u8 : [u8 ; N_CHANNELS];
    var _csv_halfWavelength @ &channelSoA.volEffect_halfWavelength  as u8 : [u8 ; N_CHANNELS];

    _csv_direction[x] = a;

    // halfWavelength
    a = 0;

// Fallthrough into __read_vol_effect_offset__dp_true__()
}


// Y = 0
// direct_page = true
// KEEP: x
// OUT: direct_page = false
func __read_vol_effect_offset__dp_true__(channelIndex: u8 in x, halfWavelength: u8 in a, subVolume: u8 in y) {
    var _csv_halfWavelength @ &channelSoA.volEffect_halfWavelength  as u8 : [u8 ; N_CHANNELS];
    var _csv_subVolume      @ &channelSoA.subVolume                 as u8 : [u8 ; N_CHANNELS];

    _csv_halfWavelength[x] = a;
    _csv_subVolume[x] = y;

    direct_page = false;

    y = 0;
    channelSoA.volEffect_offset_l[x] = a = instructionPtr[y];
    instructionPtr++;
    channelSoA.volEffect_offset_h[x] = a = instructionPtr[y];
    instructionPtr++;

    ^return process_next_bytecode(x);
}


// Y = 0
// KEEP: X
func tremolo(channelIndex: u8 in x, quarter_wavelength: u8 in a) {
    var _csv_counter        @ &channelSoA.volEffect_counter         as u8 : [u8 ; N_CHANNELS];
    var _csv_direction      @ &channelSoA.volEffect_direction       as u8 : [u8 ; N_CHANNELS];

    direct_page = true;

    _csv_direction[x] = y = VOL_PAN_EFFECT_TRIANGLE_UP;

    _csv_counter[x] = a;

    // halfWavelength
    a = a << 1;

    // subVolume
    y = 0x7f;
    return __read_vol_effect_offset__dp_true__(x, a, y);
}


// Y = 0
// KEEP: x
func pan_slide_up(channelIndex: u8 in x, ticks: u8 in a) {
    var _csp_counter        @ &channelSoA.panEffect_counter         as u8 : [u8 ; N_CHANNELS];

    direct_page = true;

    _csp_counter[x] = a;

    // Y (subPan) = 0
    return __pan_slide__dp_true__(x, VOL_PAN_EFFECT_SLIDE_UP, y);
}


// Y = 0
// KEEP: x
#[fallthrough]
func pan_slide_down(channelIndex: u8 in x, ticks: u8 in a) {
    var _csp_counter        @ &channelSoA.panEffect_counter         as u8 : [u8 ; N_CHANNELS];

    direct_page = true;

    _csp_counter[x] = a;

    a = VOL_PAN_EFFECT_SLIDE_DOWN;

    y--;
    // Y (subPan) = 0xff

// Fallthrough into __pan_slide__dp_true__()
}


// Y = 0
// direct_page = true
// KEEP: x
#[fallthrough]
func __pan_slide__dp_true__(channelIndex: u8 in x, direction: u8 in a, subPan: u8 in y) {
    var _csp_direction      @ &channelSoA.panEffect_direction       as u8 : [u8 ; N_CHANNELS];
    var _csp_halfWavelength @ &channelSoA.panEffect_halfWavelength  as u8 : [u8 ; N_CHANNELS];

    _csp_direction[x] = a;

    // halfWavelength
    a = 0;

// Fallthrough into __read_pan_effect_offset__dp_true__()
}


// Y = 0
// direct_page = true
// KEEP: x
// OUT: direct_page = false
func __read_pan_effect_offset__dp_true__(channelIndex: u8 in x, halfWavelength: u8 in a, subPan: u8 in y) {
    var _csp_halfWavelength @ &channelSoA.panEffect_halfWavelength  as u8 : [u8 ; N_CHANNELS];
    var _csp_subPan         @ &channelSoA.subPan                    as u8 : [u8 ; N_CHANNELS];

    _csp_halfWavelength[x] = a;
    _csp_subPan[x] = y;

    direct_page = false;

    y = 0;
    channelSoA.panEffect_offset_l[x] = a = instructionPtr[y];
    instructionPtr++;
    channelSoA.panEffect_offset_h[x] = a = instructionPtr[y];
    instructionPtr++;

    ^return process_next_bytecode(x);
}


// Y = 0
// KEEP: X
func panbrello(channelIndex: u8 in x, quarter_wavelength: u8 in a) {
    var _csp_counter        @ &channelSoA.panEffect_counter         as u8 : [u8 ; N_CHANNELS];
    var _csp_direction      @ &channelSoA.panEffect_direction       as u8 : [u8 ; N_CHANNELS];

    direct_page = true;

    _csp_direction[x] = y = VOL_PAN_EFFECT_TRIANGLE_UP;

    _csp_counter[x] = a;

    // halfWavelength
    a = a << 1;

    // subPan
    y = 0x7f;
    return __read_pan_effect_offset__dp_true__(x, a, y);
}



// KEEP: x
func reuse_temp_gain(channelIndex : u8 in x) {
    a = channelSoA.prevTempGain[x];
    channelSoA.virtualChannels.tempGain[x] = a;

    ^return process_next_bytecode(x);
}


// KEEP: x
func reuse_temp_gain_and_wait(channelIndex : u8 in x, ticks : u8 in a) {
    y = a;

    a = channelSoA.prevTempGain[x];
    channelSoA.virtualChannels.tempGain[x] = a;

    a = y;
    return wait(x, a);
}


// KEEP: x
func reuse_temp_gain_and_rest(channelIndex : u8 in x, ticks : u8 in a) {
    y = a;

    a = channelSoA.prevTempGain[x];
    channelSoA.virtualChannels.tempGain[x] = a;

    a = y;
    return rest(x, a);
}


// KEEP: x
func set_temp_gain(channelIndex : u8 in x, tempGain: u8 in a) {
    channelSoA.virtualChannels.tempGain[x] = a;
    channelSoA.prevTempGain[x] = a;

    ^return process_next_bytecode(x);
}


// IN: Y = 0
// KEEP: x
func set_temp_gain_and_wait(channelIndex : u8 in x, tempGain: u8 in a) {
    channelSoA.virtualChannels.tempGain[x] = a;
    channelSoA.prevTempGain[x] = a;

    // Y = 0
    a = instructionPtr[y];
    instructionPtr++;

    return wait(x, a);
}


// IN: Y = 0
// KEEP: x
#[fallthrough]
func set_temp_gain_and_rest(channelIndex : u8 in x, tempGain: u8 in a) {
    channelSoA.virtualChannels.tempGain[x] = a;
    channelSoA.prevTempGain[x] = a;

    // Y = 0
    a = instructionPtr[y];
    instructionPtr++;

// Fallthrough into rest()
}


// KEEP: x
#[fallthrough]
func rest(channelIndex : u8 in x, length: u8 in a) {
    // Y is not 0

    channelSoA.nextEventIsKeyOff[x] = y = 1;
// Fallthrough
}


// KEEP: x
func wait(channelIndex : u8 in x, length: u8 in a) {
    // Y is not 0

    channelSoA.countdownTimer[x] = a;

    // return (do not execute the next bytecode and sleep)
    return;
}

// Sets the timer for the music channels
//
// KEEP: x
func set_song_tick_clock(channelIndex : u8 in x, timer_clock: u8 in a) {
    if a < MIN_TICK_CLOCK {
        a = MIN_TICK_CLOCK;
    }

    smp.timer_0 = a;

    ^return process_next_bytecode(x);
}


const InstructionTable : [u16 ; 64] = [ f as u16 for let f in INSTRUCTIONS_WITH_ARGUMENTS ]
                                    ~ [ f as u16 for let f in NO_ARGUMENT_INSTRUCTIONS ]
                                    ~ [ disable_channel as u16 ; N_PADDING_INSTRUCTIONS ]
                                    ~ [ disable_channel as u16, ];

}

}



// Data Tables
// ===========

// Using the `code` bank so the data is included in `audio-driver.bin`
in code {
    // The S-DSP voice used by each channel
    let __VOICE_CHANNELS = [0, 1, 2, 3, 4, 5, 6, 7, 6, 7];

    // The bit used for global DSP registers (KON, KOFF, NON, EON, etc)
    const ChannelVoiceBit : [u8; N_CHANNELS] = [1 << i for let i in __VOICE_CHANNELS];

    // The address of the last writeable voice DSP register for each channel
    const LastVoiceRegister : [u8; N_CHANNELS] = [i * 0x10 | 0x07 for let i in __VOICE_CHANNELS];


    let __BC_STACK_INDEXES = [ i * BC_CHANNEL_STACK_SIZE + BC_CHANNEL_STACK_OFFSET for let i in 0..N_CHANNELS];

    const _BcStackIndexes : [u8; N_CHANNELS + 1] = __BC_STACK_INDEXES;
        let BcStackIndexesStart = &_BcStackIndexes[1] as *u8;
        let BcStackIndexesEnd = &_BcStackIndexes[0] as *u8;

    // Used to determine if `loopStackPointer` is valid.
    // Equal to `BcStackIndexesEnd[x] - 3 + 1` (+1 for < comparison)
    const ValidLoopStackPointerTable : [u8; N_CHANNELS] = [ __BC_STACK_INDEXES[i + 1] - 3 + 1 for let i in 0..(N_CHANNELS - 1)];
}

import "bytecode";

